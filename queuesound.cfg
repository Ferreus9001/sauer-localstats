// File:        queuesound.cfg
// Author:      FerreusRemedium
// Created:     2013-04-14
// Last Change: 2013-04-16

_qs_queue = []
_qs_queue_playing = 0
_qs_registered = []

_qs_getfirst = [
	local s
	s = (at $_qs_queue 0)
	_qs_queue = (sublist $_qs_queue 1)
	result $s
]

_qs_play = [
	_qs_queue_playing = 1
	local s
	s = (_qs_getfirst)
	if (!=s $s "") [
		sound (at $s 0)
		sleep (at $s 1) [
			_qs_play
		]
	] [
		_qs_queue_playing = 0
	]
]

// queueregsound <SOUND> <VOL> <LENGTH>
queueregsound = [
	local id i
	i = (listfind i $_qs_registered [
		if (=s $arg1 (at $i 0)) [
			id = (at $i 1)
			1
		]
	])
	if (< $i 0) [
		id = (registersound $arg1 $arg2)
		_qs_registered = (concat $_qs_registered [[[@@@arg1] [@@@id]]])
	]
	queuesound $id $arg3
]

// queuesound <ID> <LENGTH>
queuesound = [
	if (>= $arg2 0) [
		_qs_queue = (concat $_qs_queue [[[@@@arg1] [@@@arg2]]])
		if (= 0 $_qs_queue_playing) [
			_qs_play
		]
	] [
		sound $arg1
	]
]


// vim:set sw=4 ts=4 sts=0 noet sta sr ft=cube fdm=syntax:
